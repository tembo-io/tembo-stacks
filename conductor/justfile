VERSION := `git rev-parse HEAD`
SEMVER_VERSION := `grep version Cargo.toml | awk -F"\"" '{print $2}' | head -n 1`
NAMESPACE := "default"
KUBE_VERSION := env_var_or_default('KUBE_VERSION', '1.25.8')
RUST_LOG := "debug"

default:
  @just --list --unsorted --color=always | rg -v "    default"

install-traefik:
	helm repo add traefik https://traefik.github.io/charts
	helm repo update
	kubectl create namespace traefik || true
	helm upgrade --install --namespace=traefik --values=../coredb-operator/testdata/traefik-values.yaml traefik traefik/traefik

install-crd:
  kubectl apply -f ../charts/coredb-operator/templates/crd.yaml

# start kind
start-kind:
	kind delete cluster || true
	kind create cluster --image=kindest/node:v{{KUBE_VERSION}} --config ../coredb-operator/testdata/kind-config.yaml
	sleep 10
	kubectl wait pods --for=condition=Ready --timeout=300s --all --all-namespaces
	just install-traefik
	just install-crd
	just annotate

# annotate namespace to allow for tests
annotate:
	kubectl label namespace {{NAMESPACE}} safe-to-run-coredb-tests=true
	kubectl patch storageclass standard -p '{"allowVolumeExpansion": true}'

# format with nightly rustfmt
fmt:
	cargo clippy --fix
	cargo +nightly fmt

run-local:
	POSTGRES_QUEUE_CONNECTION=postgresql://postgres:postgres@0.0.0.0:5431/postgres \
	RUST_BACKTRACE=1 \
	RUST_LOG={{RUST_LOG}} \
	CONTROL_PLANE_EVENTS_QUEUE=myqueue_control_plane \
	DATA_PLANE_EVENTS_QUEUE=myqueue_data_plane \
	cargo watch -x run

run-postgres:
	docker run -d --name pgmq-pg -e POSTGRES_PASSWORD=postgres -p 5431:5432 quay.io/coredb/pgmq-pg:latest

run-tests:
	echo "Running unit tests"
	cargo test -- --nocapture
	echo "Running functional tests"
	cargo test -- --nocapture --ignored
